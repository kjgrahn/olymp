#!/usr/bin/perl -w # -*- perl -*-
#
# $Id: olymp,v 1.8 2005-07-07 18:26:57 grahn Exp $
# 

use strict;
use POSIX qw(strftime);
use vars qw($opt_e);
use Getopt::Std;

my %monthcodes = ("1" => 1, "2" => 2, "3" => 3,
		  "4" => 4, "5" => 5, "6" => 6,
		  "7" => 7, "8" => 8, "9" => 9,
		  "a" =>10, "b" =>11, "c" =>12);

getopts('e') or die;

while(my $file = shift) {

    my ($prefix, $year, $month, $day, $index);

    if($file =~ /(.*?)p([0-9abc])(\d{2})(\d{4})\.jpg/) {

	($prefix, $month, $day, $index) = ($1, $monthcodes{$2}, $3, $4);
	$year = 2005;
    }
    elsif($file =~ m|(.*?/)?(\d{2})(\d{2})(\d{2})\_(\d{2})\.jpg|) {

	($prefix, $year, $month, $day, $index) = ($1, 2000+$2, $3, $4, $5);
    }
    elsif($file =~ m|(.*?/)?(20\d{2})-(\d{2})-(\d{2})\_(\d{4})\.jpg|) {

	($prefix, $year, $month, $day, $index) = ($1, $2, $3, $4, $5);
    }
    else {
	next;
    }

    next if $month>12 or $day>31;
    next unless -e $file;

    # find its creation time

    my ($dev,$ino,$mode,$nlink,$uid,$gid,$rdev,$size,$atime,$mtime,$ctime) =
	stat $file
	    or die;

    my $date = sprintf "%04d-%02d-%02d", $year, $month, $day;

    # if it matches the date in the file name, we can trust the time, too
    my $hours = "00:00";
    $hours = strftime "%H:%M", localtime $mtime
	if (sprintf "%04d%02d%02d", $year, $month, $day)
	    eq (strftime "%Y%m%d", localtime $mtime);

    my  $new = sprintf "%s_%04d.jpg", $date, $index;
    print "$new\n";
    print "$date $hours\n\n";

    if(defined($opt_e)) {
	# we want to rename as well
	rename($file, "$prefix$new") unless -e "$prefix$new";
    }
}

__END__

=head1 NAME

olymp - enumerate and/or rename Olympus digital camera photos

=head1 SYNOPSIS

B<olymp>
[ B<-e> ]
I<file>
...

=head1 DESCRIPTION

Olympus cameras (at least the Camedia C-7x0 UZ series)
store images in files with pretty silly names, like B<p7050124.jpg>.
This translates to
"the current year,
month 7,
day 05,
image serial number 0124".

This utility prints a more readable variation of the file name,
along with the date and time.
Optionally, if the B<-e> option is given,
the file is renamed in the same fashion.

The time stamp of the file is used for correlation, and for
finding the time of day the photos were taken.
The camera stores a correct (if you configure it correctly)
time stamp; it is up to you to preserve it when copying the file.

The generated file names look like
B<2005-07-06_0018.jpg>.
This assures that they sort correctly in e.g. directory listing.

=head1 NOTES

Earlier versions of this script generated file names on the form
B<050706_18.jpg>.
These file names are accepted, just like the original Olympus file names
(and the new file names for that matter).

All this assumes you I<do> have a correctly set camera system clock,
and have retained the original time stamp on the file.

=head1 BUGS

Should I<really> use the EXIF information inside the image
(still in combination with the file name).

=head1 AUTHOR

Jörgen Grahn I<E<lt>jgrahn@algonet.seE<gt>>

=cut
