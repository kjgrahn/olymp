#!/usr/bin/perl -w # -*- perl -*-
#
# $Id: olymp,v 1.6 2005-01-06 15:31:50 grahn Exp $
# 

use strict;
use POSIX qw(strftime);
use vars qw($opt_e);
use Getopt::Std;

my %monthcodes = ("1" => 1, "2" => 2, "3" => 3,
		  "4" => 4, "5" => 5, "6" => 6,
		  "7" => 7, "8" => 8, "9" => 9,
		  "a" =>10, "b" =>11, "c" =>12);

getopts('e') or die;

while(my $file = shift) {

    my ($prefix, $year, $month, $day, $index);

    if($file =~ /(.*?)p([0-9abc])(\d{2})(\d{4})\.jpg/) {

	($prefix, $month, $day, $index) = ($1, $monthcodes{$2}, $3, $4);
	$year = 5;
    }
    elsif($file =~ /(.*?)(\d{2})(\d{2})(\d{2})\_(\d{2})\.jpg/) {

	($prefix, $year, $month, $day, $index) = ($1, $2, $3, $4, $5);
    }
    else {
	next;
    }

    next if $month>12 or $day>31 or $index>99;
    next unless -e $file;

    # find its creation time

    my ($dev,$ino,$mode,$nlink,$uid,$gid,$rdev,$size,$atime,$mtime,$ctime) =
	stat $file
	    or die;

    # if it matches the date in the file name, we can trust the time, too
    my $hours = "00:00";
    $hours = strftime "%H:%M", localtime $mtime
	if (sprintf "%02d%02d%02d", $year, $month, $day)
	    eq (strftime "%y%m%d", localtime $mtime);

    my  $new = sprintf "%02d%02d%02d_%02d.jpg", $year, $month, $day, $index;
    print "$new\n";
    print "$hours\n\n";

    if(defined($opt_e)) {
	# we want to rename as well
	rename($file, "$prefix$new") unless -e "$prefix$new";
    }
}
